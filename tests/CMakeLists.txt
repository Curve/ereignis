cmake_minimum_required(VERSION 3.25)
project(ereignis-tests LANGUAGES CXX)

# --------------------------------------------------------------------------------------------------------
# Enable sanitizers
# --------------------------------------------------------------------------------------------------------

if (NOT WIN32)
    add_compile_options(-fsanitize=address)
    add_link_options(-fsanitize=address)
endif()

# --------------------------------------------------------------------------------------------------------
# Create executable
# --------------------------------------------------------------------------------------------------------

add_executable(${PROJECT_NAME})

target_compile_features(${PROJECT_NAME} PRIVATE cxx_std_23)
set_target_properties(${PROJECT_NAME} PROPERTIES CXX_STANDARD 23 CXX_EXTENSIONS OFF CXX_STANDARD_REQUIRED ON)

if (NOT MSVC)
    target_compile_options(${PROJECT_NAME} PRIVATE -Wall -Wextra -Wpedantic -Werror -pedantic -pedantic-errors -Wfatal-errors)
endif()

# --------------------------------------------------------------------------------------------------------
# Add Sources
# --------------------------------------------------------------------------------------------------------

file(GLOB src "*.cpp")
target_sources(${PROJECT_NAME} PRIVATE ${src})

# --------------------------------------------------------------------------------------------------------
# Link Dependencies 
# --------------------------------------------------------------------------------------------------------

include("../cmake/cpm.cmake")

CPMFindPackage(
  NAME           ut
  VERSION        2.3.1
  GIT_REPOSITORY "https://github.com/boost-ext/ut"
  OPTIONS        "BOOST_UT_DISABLE_MODULE ON"
)

CPMFindPackage(
  NAME           fill
  VERSION        1.0.0
  GIT_REPOSITORY "https://github.com/saucer/fill"
  OPTIONS        "fill_namespace std" "fill_silent ON"
)

target_link_libraries(${PROJECT_NAME} PRIVATE Boost::ut saucer::fill cr::ereignis)

# --------------------------------------------------------------------------------------------------------
# Add Test-Target
# --------------------------------------------------------------------------------------------------------

include(CTest)

add_test(NAME ereignis-tests COMMAND $<TARGET_FILE:${PROJECT_NAME}>)
